function( detect_system_pybind11 resvar_cflags_list resvar_linkflags_list )
  set( cmd "${PYTHON_EXECUTABLE}" -mpybind11 --cmakedir )
  if ( DG_VERBOSE )
    string( JOIN " " tmp ${cmd} )
    message("-- Invoking:" ${tmp})
  else()
    list( APPEND cmd ERROR_QUIET )
  endif()
  execute_process( COMMAND ${cmd}
    OUTPUT_VARIABLE pybind11_DIR
    OUTPUT_STRIP_TRAILING_WHITESPACE
    RESULT_VARIABLE cmd_exitcode
    )
  if ( NOT "x${cmd_exitcode}" STREQUAL "x0" )
    message( FATAL_ERROR "Could not find pybind11." )
  endif()

  set( findpkgargs pybind11 2.10.4 NO_MODULE NO_DEFAULT_PATH )#TODO: If found but version is too old, and in conda, provide conda/pip command for updating ncrystal version.
  if ( DG_VERBOSE )
    message("-- Found pybind11_DIR=${pybind11_DIR}")
    string( JOIN " " tmp ${findpkgargs} )
    message("-- Trying to invoke find_package( ${tmp} )." )
  endif()
  find_package( ${findpkgargs} )
  if ( NOT pybind11_FOUND )
    if ( DG_VERBOSE )
      message("-- The find_package call failed.")
    endif()
    message( FATAL_ERROR "-- DGCODE_USECONDABOOSTPYTHON==PYBIND11 but could not find pybind11 package." )
  endif()
  message("-- Found pybind11 version ${pybind11_VERSION}")


  set( tmp_cflags_list "" )
  foreach( tmp ${pybind11_INCLUDE_DIRS} )
    list(APPEND tmp_cflags_list "-isystem${tmp}" )
  endforeach()
  foreach( tmp ${pybind11_DEFINITIONS} )
    list(APPEND tmp_cflags_list "-D${tmp}" )
  endforeach()
  set( tmp_linkflags_list "" )
  foreach( tmp ${pybind11_LIBRARIES} )
    list(APPEND tmp_linkflags_list "${tmp}" )
  endforeach()
  set( ${resvar_cflags_list} "${tmp_cflags_list}" PARENT_SCOPE )
  set( ${resvar_linkflags_list} "${tmp_linkflags_list}" PARENT_SCOPE )
endfunction()

detect_system_pybind11( SYSTEM_BOOSTPYTHON_CFLAGS_LIST SYSTEM_BOOSTPYTHON_LINKFLAGS_LIST )
set( SYSTEM_BOOSTPYTHON_INCDIR "" )
set( SYSTEM_BOOSTPYTHON_FOUND ON )
if ( DG_VERBOSE )
  message("-- pybind11 compilation flags: ${SYSTEM_BOOSTPYTHON_CFLAGS_LIST} " )
  message("-- pybind11 link flags: ${SYSTEM_BOOSTPYTHON_LINKFLAGS_LIST} " )
  #message("-- pybind11 headers : ${SYSTEM_BOOSTPYTHON_INCDIR} " )
endif()
